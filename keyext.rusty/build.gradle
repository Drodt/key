plugins {
    id 'java'
    id 'application'
}

group = 'org.key_project'
version = '2.13.0'

configurations { antlr4 }

repositories {
    mavenCentral()
}

dependencies {
    implementation('com.google.code.gson:gson:2.11.0')
    testImplementation(platform('org.junit:junit-bom:5.10.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    antlr4 "org.antlr:antlr4:4.13.2"
    api "org.antlr:antlr4-runtime:4.13.2"
    api(project(':key.ncore'))
    api(project(':key.ncore.calculus'))
    api(project(':key.util'))

    implementation('info.picocli:picocli:4.7.7')
    annotationProcessor('info.picocli:picocli-codegen:4.7.7')
}

test {
    useJUnitPlatform()
}

//generateGrammarSource {
//    arguments += ["-visitor", "-lib" , "../key.ncore/src/main/antlr/"]
//}

def antlr4OutputKey = String.valueOf(projectDir) + '/build/generated-src/antlr/main/org/key_project/rusty/parser'

sourceSets.main.java.srcDirs(String.valueOf(projectDir) + '/build/generated-src/antlr/main/')

tasks.register('runAntlr4Key', JavaExec) {
    //see incremental task api, prevents rerun if nothing has changed.
    inputs.files "src/main/antlr/KeYRustyLexer.g4", "src/main/antlr/RustyLexer.g4",
                 "src/main/antlr/RustySchemaLexer.g4",
                 "src/main/antlr/KeYRustyParser.g4", "src/main/antlr/RustyParser.g4",
                 "src/main/antlr/RustySchemaParser.g4"
    outputs.dir antlr4OutputKey
    classpath = configurations.antlr4
    mainClass.set("org.antlr.v4.Tool")
    args = ["-visitor",
            "-lib", "../key.ncore/src/main/antlr/",
            "-Xexact-output-dir", "-o", antlr4OutputKey,
            //"-package", "org.key_project.rusty.parser",
            "src/main/antlr/KeYRustyLexer.g4", "src/main/antlr/RustyLexer.g4",
            "src/main/antlr/RustySchemaLexer.g4",
            "src/main/antlr/KeYRustyParser.g4", "src/main/antlr/RustyParser.g4",
            "src/main/antlr/RustySchemaParser.g4"]
    doFirst {
        file(antlr4OutputKey).mkdirs()
        println("create $antlr4OutputKey")
    }
}
compileJava.dependsOn runAntlr4Key

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

application {
    mainClass.set('org.key_project.rusty.CLI')
}

tasks.register('testRunAllProofs', Test) {
    description = 'Prove/reload all .key/.proof files tagged for regression testing'
    group = "verification"
    filter {
        includeTestsMatching("RunAllProofs")
    }
}

checkerFramework {
    if(System.getProperty("ENABLE_NULLNESS")) {
        checkers = [
                "org.checkerframework.checker.nullness.NullnessChecker",
        ]
        extraJavacArgs = [
                "-AonlyDefs=^org\\.key_project\\.rusty\\.parser\\.builder",
                "-AassumeAssertionsAreEnabled",
                "-Xmaxerrs", "1000",
                "-Astubs=$rootDir/key.util/src/main/checkerframework:permit-nullness-assertion-exception.astub",
                "-AstubNoWarnIfNotFound",
                "-Werror",
                "-Aversion",
        ]
    }
}