/* This file is part of KeY - https://key-project.org
 * KeY is licensed under the GNU General Public License Version 2
 * SPDX-License-Identifier: GPL-2.0-only */

\schemaVariables {
    \term bool bo, b1, b2;
}

\rules {
    bool_equal {
        \find(bo = bo)
        \replacewith(true)
        \heuristics(simplify_bool, concrete)
    };

    bool_equal_2 {
        \find(b1 = TRUE <-> b2 = TRUE)
        \replacewith(b1 = b2)
        \heuristics(simplify_bool, concrete)
        \displayname "bool_equal"
    };

    bool_not_equal_1 {
        \find(TRUE = FALSE)
        \replacewith(false)
        \heuristics(simplify_bool, concrete)
        \displayname "bool_not_equal"
    };
    bool_not_equal_2 {
        \find(FALSE = TRUE)
        \replacewith(false)
        \heuristics(simplify_bool, concrete)
        \displayname "bool_not_equal"
    };

    true_to_not_false {
        \find(bo = TRUE)
        \replacewith(!bo = FALSE)
    };

    // anything to ...=TRUE:

    false_to_not_true {
        \find(bo = FALSE)
        \replacewith(!bo = TRUE)
        \heuristics(simplify_bool, concrete)
    };

    bool_true_commute {
        \find(TRUE = bo)
        \replacewith(bo = TRUE)
        \heuristics(simplify_bool)
        \displayname "bool commute"
    };

    bool_false_commute {
        \find(FALSE = bo)
        \replacewith(bo = FALSE)
        \heuristics(simplify_bool)
        \displayname "bool commute"
    };

    ex_bool {
        \schemaVar \variables bool x;
        \schemaVar \formula c;
        \find(\exists x; c)
        \replacewith({\subst x; FALSE}c | {\subst x; TRUE}c)
        \heuristics(bool_cases)
    };

    all_bool {
        \schemaVar \variables bool x;
        \schemaVar \formula c;
        \find(\forall x; c)
        \replacewith({\subst x; FALSE}c & {\subst x; TRUE}c)
        \heuristics(bool_cases)
    };

    ///////////////////////////////////////////////////////////////////////////////

    apply_eq_bool {
        \assumes( ==> bo = TRUE)
        \find(bo)
        \sameUpdateLevel
        \replacewith(FALSE)
        \heuristics(apply_equations)
    };

    apply_eq_bool_2 {
        \assumes( ==> bo = FALSE)
        \find(bo)
        \sameUpdateLevel
        \replacewith(TRUE)
        \heuristics(apply_equations)
        \displayname "apply_eq_bool"
    };

    apply_eq_bool_rigid {
        \schemaVar \term [rigid] bool br;
        \assumes( ==> br = TRUE)
        \find(br)
        \replacewith(FALSE)
        \heuristics(apply_equations)
        \displayname "apply_eq_bool"
    };

    apply_eq_bool_rigid_2 {
        \schemaVar \term [rigid] bool br;
        \assumes( ==> br = FALSE)
        \find(br)
        \replacewith(TRUE)
        \heuristics(apply_equations)
        \displayname "apply_eq_bool"
    };

    \lemma
    ifthenelse_equals {
        \schemaVar \formula b, c;
        \find(\if(b) \then(TRUE) \else(FALSE) = \if(c) \then(TRUE) \else(FALSE))
        \replacewith(b <-> c)
        \heuristics(concrete)
    };

    \lemma
    ifthenelse_equals_1 {
        \schemaVar \formula b;
        \schemaVar \term bool c;
        \find(\if(b) \then(TRUE) \else(FALSE) = c)
        \replacewith(b <-> c = TRUE)
    };

    \lemma
    ifthenelse_equals_2 {
        \schemaVar \term bool b;
        \schemaVar \formula c;
        \find(b = \if(c) \then(TRUE) \else(FALSE))
        \replacewith(b = TRUE <-> c)
    };
}
